apply plugin: 'java-library'
apply plugin: 'osgi'
apply plugin: 'maven-publish'
apply plugin: 'optional-base'
apply plugin: 'com.jfrog.artifactory'

sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

jar {
    sourceSets {
        main {
            java.srcDir '../src'
            resources.srcDirs '../jniLibs'
        }
    }

    manifest {
        instruction 'Automatic-Module-Name', 'org.mongodb.driver.embedded.client.capi'
        instruction 'Build-Version', getGitVersion()
        instruction 'Import-Package',
                     'com.mongodb.*'
    }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    classifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}

dependencies {
    api "net.java.dev.jna:jna:$jnaVersion"
    api 'org.slf4j:slf4j-api:1.7.6', optional
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId = libraryName

            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = pomName
                description = pomDescription
                url = pomUrl
                licenses {
                    license {
                        name = pomLicenseName
                        url = pomLicenseUrl
                    }
                }
                scm {
                    connection = "scm:" + pomScmConnection
                    developerConnection = "scm:" + pomScmDevConnection
                    url = pomScmUrl
                }
            }
        }
    }

    repositories {
        maven {
            name = 'local'
            url = "$buildDir/repo"
        }
    }
}


artifactory {
    contextUrl = "http://artifactory.corp.mongodb.com/artifactory"
    publish {
        repository {
            repoKey = 'mongodb-mobile-local'
            username = System.getenv('artifactory_mobile_user')
            password = System.getenv('artifactory_mobile_pass')
            maven = true
        }
        defaults {
            publications ('maven')
        }
    }
    resolve {
        repository {
            repoKey = 'maven'
            username = System.getenv('artifactory_mobile_user')
            password = System.getenv('artifactory_mobile_pass')
            maven = true
        }
    }
}
